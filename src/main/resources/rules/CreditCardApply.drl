package CreditCardApply;

import com.quming.entity.CreditCardApplyInfo;

/*
 *用户合法性检查规则,四个规则
 */
 /*规则名字，任意写，只要在当前文件内唯一即可，可以写中文*/
rule "如果申请人既没房也没车，同时学历大专以下，月薪少于5000，申请不通过"
    salience 10          //指定优先级，数值越大优先级越高，不指定的情况下由上到下执行
    no-loop true         //防止陷入死循环
    when
        /*规则条件，到工作内存中查找CreditCardApplyInfo对象
        里面出来的结果只能是ture或者false
        $c是绑定变量名，可以任意命名，官方推荐$符号，定义了绑定变量名，可以在then部分操作fact对象*/
        $c:CreditCardApplyInfo(hasHouse == false && hasCar == false &&
                                 education == CreditCardApplyInfo.EDUCATION_1 &&
                                    monthlyIncome<5000)
    then
        $c.setCheckResult(false);      //审核不通过
        drools.halt();                 //终止规则引擎执行,后续规则不再执行
        System.out.println("如果申请人既没房也没车，同时学历大专以下，月薪少于5000，申请不通过");
end

rule "如果申请人既没房也没车，同时学历大专或本科，月薪少于3000，申请不通过"
    salience 10
    no-loop true
    when
        $c:CreditCardApplyInfo(hasHouse == false && hasCar == false &&
                                 (education == CreditCardApplyInfo.EDUCATION_2 ||
                                     education == CreditCardApplyInfo.EDUCATION_3) &&
                                   monthlyIncome<3000)
    then
        $c.setCheckResult(false);
        drools.halt();
end

rule "如果申请人既没房也没车，同时学历本科以上，月薪少于2000，同时之前没有过信用卡，申请不通过"
    salience 10
    no-loop true
    when
        $c:CreditCardApplyInfo(hasHouse == false && hasCar == false &&
                                 education == CreditCardApplyInfo.EDUCATION_4 &&
                                    monthlyIncome<2000 &&  hasCreditCardCount == 0)
    then
        $c.setCheckResult(false);
        drools.halt();
end

rule "申请人现有信用卡数量大于10，申请不通过"
    salience 10
    no-loop true
    when
        $c:CreditCardApplyInfo(hasCreditCardCount > 10)
    then
        $c.setCheckResult(false);
        drools.halt();
end

/*
 *确定信用卡额度,五个规则
 */
rule "如果申请人有房有车，或者月收入在20000以上，发放信用卡的额度是15000"
   salience 1
   no-loop true
   activation-group "quota_group"       //同一个组里面的规则是互斥的
   when
       $c:CreditCardApplyInfo(checkResult==true &&
                           (hasHouse == true && hasCar == true) ||
                            (monthlyIncome>20000))
   then
       $c.setQuota(15000);
end

rule "如果申请人没房没车，但月收入在10000—20000之间，发放信用卡的额度是6000"
   salience 1
   no-loop true
   activation-group "quota_group"
   when
       $c:CreditCardApplyInfo(checkResult==true &&
                           hasHouse == false && hasCar == false &&
                            monthlyIncome>10000 && monthlyIncome<20000)
   then
       $c.setQuota(6000);
end

rule "如果申请人没房没车，但月收入在10000以下，发放信用卡的额度是3000"
   salience 1
   no-loop true
   activation-group "quota_group"
   when
       $c:CreditCardApplyInfo(checkResult==true &&
                           hasHouse == false && hasCar == false &&
                            monthlyIncome<10000)
   then
       $c.setQuota(3000);
end

rule "如果申请人有房没车或者没房有车，但月收入在10000以下，发放信用卡的额度是5000"
   salience 1
   no-loop true
   activation-group "quota_group"
   when
       $c:CreditCardApplyInfo(checkResult==true &&
                           ((hasHouse == true && hasCar == false) ||
                              (hasHouse == false && hasCar == true)) &&
                                 monthlyIncome<10000)
   then
       $c.setQuota(5000);
end

rule "如果申请人有房没车或者没房有车，但月收入在10000—20000之间，发放信用卡的额度是8000"
   salience 1
   no-loop true
   activation-group "quota_group"
   when
       $c:CreditCardApplyInfo(checkResult==true &&
                           ((hasHouse == true && hasCar == false) ||
                            (hasHouse == false && hasCar == true))  &&
                            monthlyIncome>10000 && monthlyIncome<20000)
   then
       $c.setQuota(8000);
end